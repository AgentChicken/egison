;;;;;
;;;;;
;;;;; Term Rewriting
;;;;;
;;;;;

;(set-term-rewriting-rule {[(* (sqrt $x) (sqrt $y)) (sqrt (* x y))]
;                          [(rt n x)^n x]
;                          [w^3 1]
;                          [w^2 (- w -1)]
;                          [i^2 -1]})

(define $math-normalize
  (lambda [$mexpr]
    (if (number? mexpr)
      (if (rational? mexpr)
        mexpr
        (map-terms rewrite-rule-rt (map-terms rewrite-rule-sqrt (map-terms rewrite-rule-i mexpr))))
      mexpr)))

(define $map-terms
  (lambda [$fn $mexpr]
    (match mexpr math-expr
      {[<div <plus $ts1> <plus $ts2>>
        (/' (foldl +' 0 (map fn ts1))
            (foldl +' 0 (map fn ts2)))]})))

(define $rewrite-rule-i
  (lambda [$term]
    (match term term-expr
      {[<term $a <ncons (& ?even? $k) <symbol ,"i"> $ts>>
        (*' a (**' -1 (quotient k 2)) (foldl *' 1 (map 2#(**' %1 %2) ts)))]
       [<term $a <ncons $k <symbol ,"i"> $ts>>
        (*' a (**' -1 (quotient k 2)) i (foldl *' 1 (map 2#(**' %1 %2) ts)))]
       [_ term]})))

(define $rewrite-rule-sqrt
  (lambda [$term]
    (match term term-expr
      {[<term $a <cons <apply ,"sqrt" <cons $x <nil>>> <cons <apply ,"sqrt" <cons $y <nil>>> $ts>>>
        (*' a (sqrt (*' x y)) (foldl *' 1 (map 2#(**' %1 %2) ts)))]
       [_ term]})))

(define $rewrite-rule-rt
  (lambda [$term]
    (match term term-expr
      {[<term $a <cons <apply ,"rt" <cons $n <cons $x <nil>>>> <cons <apply ,"rt" <cons ,n <cons $y <nil>>>> $ts>>>
        (*' a (rt n (*' x y)) (foldl *' 1 (map 2#(**' %1 %2) ts)))]
       [_ term]})))
