;;;
;;; Polar coordinates
;;;

(define $x [|r θ φ|])

(define $X [|(* r (sin θ) (cos φ)) ; = x
             (* r (sin θ) (sin φ)) ; = y
             (* r (cos θ))         ; = z
             |])

;;
;; Local coordinates
;;

(define $e ((∂/∂ X~# $) x_#))
e

;;
;; Metric tensor
;;

(define $g__ (generate-tensor 2#(V.* e_%1 e_%2) {3 3}))
(define $g~~ (with-symbols {i j} (/ (unit-tensor {3 3})_i_j g_i_j)))

g_#_#;
g~#~#;

;;
;; Christoffel symbols of the first kind
;;

(define $Γ___
  (with-symbols {j k l}
    (* (/ 1 2)
       (+ (∂/∂ g_j_l x_k)
          (∂/∂ g_j_k x_l)
          (* -1 (∂/∂ g_k_l x_j))))))

Γ_#_#_#;
Γ_1_#_#;
Γ_2_#_#;
Γ_3_#_#;

;;
;; Christoffel symbols of the sedond kind
;;

(define $Γ~__
  (with-symbols {i j k l}
    (. g~i~j Γ_j_k_l)))

Γ~#_#_#;
Γ~1_#_#;
Γ~2_#_#;
Γ~3_#_#;

;;
;; Covariant derivative
;;

; 1

(define $tmp1 (. g~i~j (∂/∂ (∂/∂ (f r θ φ) x_j) x_i)))
(define $tmp2 (. (. g~i~j Γ~k_i_j) (∂/∂ (f r θ φ) x_k)))
tmp1;(/ (+ (* (f_1_1 r θ φ) r^2 (sin θ)^2) (* (f_2_2 r θ φ) (sin θ)^2) (f_3_3 r θ φ)) (* r^2 (sin θ)^2))
tmp2;(/ (+ (* -2 (f_1 r θ φ) r (sin θ)) (* -1 (cos θ) (f_2 r θ φ))) (* r^2 (sin θ)))
(- tmp1 tmp2)
;(/ (+ (* (f_1_1 r θ φ) r^2 (sin θ)^2) (* (f_2_2 r θ φ) (sin θ)^2) (f_3_3 r θ φ) (* 2 (f_1 r θ φ) r (sin θ)^2) (* (cos θ) (f_2 r θ φ) (sin θ))) (* r^2 (sin θ)^2))

; 2

(define $sqrt-g (sqrt (M.det g_#_#)))
sqrt-g;(* r^2 (sin θ))

(/ (contract + (∂/∂ (* sqrt-g (. g~i~j (∂/∂ (f r θ φ) x_j))) x_i)) sqrt-g)
;(/ (+ (* 2 r (sin θ)^2 (f_1 r θ φ)) (* r^2 (sin θ)^2 (f_1_1 r θ φ)) (* (cos θ) (f_2 r θ φ) (sin θ)) (* (sin θ)^2 (f_2_2 r θ φ)) (f_3_3 r θ φ)) (* (sin θ)^2 r^2))







