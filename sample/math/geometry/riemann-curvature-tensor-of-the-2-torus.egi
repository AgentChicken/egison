;;;
;;; Coordinates for Torus
;;;

(define $x [|θ φ|])

(define $X [|(* '(+ (* a (cos θ)) b) (cos φ)) ; = x
             (* '(+ (* a (cos θ)) b) (sin φ)) ; = y
             (* a (sin θ))                    ; = z
             |])

;;
;; Local coordinates
;;

(define $e ((∂/∂ X~# $) x~#))
e
;[|[| (* -1 a (sin θ) (cos φ)) (* -1 a (sin θ) (sin φ)) (* a (cos θ)) |]
;  [| (* -1 '(+ (* a (cos θ)) b) (sin φ)) (* '(+ (* a (cos θ)) b) (cos φ)) 0 |]
;  |]~#~#

;;
;; Metric tensor
;;

(define $g__ (generate-tensor 2#(V.* e_%1 e_%2) {2 2}))
(define $g~~ (with-symbols {i j} (/ (unit-tensor {2 2})_i_j g_i_j)))

g_#_#;[| [| a^2 0 |] [| 0 '(+ (* a (cos θ)) b)^2 |] |]_#_#
g~#~#;[| [| (/ 1 a^2) 0 |] [| 0 (/ 1 '(+ (* a (cos θ)) b)^2) |] |]~#~#

;;
;; Christoffel symbols
;;

(define $Γ~__ (generate-tensor
                3#(with-symbols {j}
                    (* (/ 1 2) (. g~%1~j (+ (∂/∂ g_j_%3 x~%2) (∂/∂ g_j_%2 x~%3) (* -1 (∂/∂ g_%2_%3 x~j))))))
                {2 2 2}))

Γ~#_#_#;(tensor {2 2 2} {0 0 0 (/ (* '(+ (* a (cos θ)) b) (sin θ)) a) 0 (/ (* -1 a (sin θ)) '(+ (* a (cos θ)) b)) (/ (* -1 a (sin θ)) '(+ (* a (cos θ)) b)) 0} )~#_#_#
Γ~1_#_#;[| [| 0 0 |] [| 0 (/ (* '(+ (* a (cos θ)) b) (sin θ)) a) |] |]_#_#
Γ~2_#_#;[| [| 0 (/ (* -1 a (sin θ)) '(+ (* a (cos θ)) b)) |] [| (/ (* -1 a (sin θ)) '(+ (* a (cos θ)) b)) 0 |] |]_#_#

;;
;; Riemann curvature tensor
;;

(define $R~___ (generate-tensor
                 4#(with-symbols {j}
                     (+ (- (∂/∂ Γ~%1_%2_%3 x~%4) (∂/∂ Γ~%1_%2_%4 x~%3))
                        (- (. Γ~j_%2_%3 Γ~%1_j_%4) (. Γ~j_%2_%4 Γ~%1_j_%3))
                        ))
                 {2 2 2 2}))

R~#_#_#_#;(tensor {2 2 2 2} {0 0 0 0 0 (/ (* -1 '(+ (* a (cos θ)) b) (cos θ)) a) (/ (* '(+ (* a (cos θ)) b) (cos θ)) a) 0 0 (/ (* a (cos θ)) '(+ (* a (cos θ)) b)) (/ (* -1 a (cos θ)) '(+ (* a (cos θ)) b)) 0 0 0 0 0} )~#_#_#_#
R~1_1_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#
R~1_2_#_#;[| [| 0 (/ (* -1 '(+ (* a (cos θ)) b) (cos θ)) a) |] [| (/ (* '(+ (* a (cos θ)) b) (cos θ)) a) 0 |] |]_#_#
R~2_1_#_#;[| [| 0 (/ (* a (cos θ)) '(+ (* a (cos θ)) b)) |] [| (/ (* -1 a (cos θ)) '(+ (* a (cos θ)) b)) 0 |] |]_#_#
R~2_2_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#

(define $R____ (with-symbols {i} (. g_i_# R~i_#_#_#)))

R_#_#_#_#;(tensor {2 2 2 2} {0 0 0 0 0 (* -1 a '(+ (* a (cos θ)) b) (cos θ)) (* a '(+ (* a (cos θ)) b) (cos θ)) 0 0 (* '(+ (* a (cos θ)) b) a (cos θ)) (* -1 '(+ (* a (cos θ)) b) a (cos θ)) 0 0 0 0 0} )_#_#_#_#
R_1_1_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#
R_1_2_#_#;[| [| 0 (* -1 a '(+ (* a (cos θ)) b) (cos θ)) |] [| (* a '(+ (* a (cos θ)) b) (cos θ)) 0 |] |]_#_#
R_2_1_#_#;[| [| 0 (* '(+ (* a (cos θ)) b) a (cos θ)) |] [| (* -1 '(+ (* a (cos θ)) b) a (cos θ)) 0 |] |]_#_#
R_2_2_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#

;;
;; Ricci curvature
;;

(define $Ric__ (with-symbols {i j k} (contract + R~i_j_k_i)))

Ric_#_#;[| [| (/ (* a (cos θ)) '(+ (* a (cos θ)) b)) 0 |] [| 0 (/ (* '(+ (* a (cos θ)) b) (cos θ)) a) |] |]_#_#

;;
;; Scalar curvature
;;

(define $scalar-curvature (with-symbols {j k} (. g~j~k Ric_j_k)))

scalar-curvature;(/ (* 2 (cos θ)) (* a '(+ (* a (cos θ)) b)))

;;
;; Covariant derivative of Riemann curvature tensor
;;

(define $∇R_____
  (with-symbols {i j k l m n}
    (- (∂/∂ R_i_j_k_l x_m)
       (. Γ~n_m_i R_n_j_k_l)
       (. Γ~n_m_j R_i_n_k_l)
       (. Γ~n_m_k R_i_j_n_l)
       (. Γ~n_m_l R_i_j_k_n))))

∇R_#_#_#_#_#
;(tensor {2 2 2 2 2} {0 0 0 0 0 0 0 0 0 0 (+ (* a '(+ (* a (cos θ)) b) (sin θ)) (* -1 a^2 (sin θ) (cos θ))) 0 (+ (* -1 a '(+ (* a (cos θ)) b) (sin θ)) (* a^2 (sin θ) (cos θ))) 0 0 0 0 0 (+ (* -1 '(+ (* a (cos θ)) b) a (sin θ)) (* a^2 (sin θ) (cos θ))) 0 (+ (* '(+ (* a (cos θ)) b) a (sin θ)) (* -1 a^2 (sin θ) (cos θ))) 0 0 0 0 0 0 0 0 0 0 0} )_#_#_#_#_#

;;
;; Second Bianchi identity
;;

(with-symbols {i j k l m} (+ ∇R_i_j_k_l_m ∇R_i_j_l_m_k ∇R_i_j_m_k_l))
;(tensor {2 2 2 2 2} {0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0} )
