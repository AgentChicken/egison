(define $g [| [| -1 0 0 0 |] [| 0 1 0 0 |] [| 0 0 1 0 |] [| 0 0 0 1 |] |])

(define $d
  (lambda [%X]
    !((flip ∂/∂) [| t x y z |] X)))

(define $hodge0
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 0)_i1_i2_i3_i4 A)))))

(define $hodge1
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 1)_i1_i2_i3_i4 A_j1 g~i1~j1)))))

(define $hodge2
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 2)_i1_i2_i3_i4 A_j1_j2 g~i1~j1 g~i2~j2)))))

(define $hodge3
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2 j3}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 3)_i1_i2_i3_i4 A_j1_j2_j3 g~i1~j1 g~i2~j2 g~i3~j3)))))

(define $hodge4
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2 j3}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 4)_i1_i2_i3_i4 A_j1_j2_j3_j4 g~i1~j1 g~i2~j2 g~i3~j3 g~i4~j4)))))

(define $hodge
  (lambda [%A]
    (match (tensor-order A) integer
      {[,0 (hodge0 A)]
       [,1 (hodge1 A)]
       [,2 (hodge2 A)]
       [,3 (hodge3 A)]
       [,4 (hodge4 A)]})))

(define $δ
  (lambda [%A]
    (hodge (d (hodge A)))))

(define $grad d)
(define $rot d)
(define $div δ)

(define $Δ
  (lambda [%A]
    (match (tensor-order A) integer
      {[,0 (δ (d A))]
       [,4 (d (δ A))]
       [_ (+ (d (δ A)) (δ (d A)))]})))

(define $normalize1
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1}
      (. (ε 4)~i1~i2~i3~i4 A_i1 (ε 4)_i2_i3_i4_j1))))

(define $normalize2
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2}
      (. (/ 1 2) (ε 4)~i1~i2~i3~i4 A_i1_i2 (ε 4)_i3_i4_j1_j2))))

(define $normalize3
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2 j3}
      (. (/ 1 6) (ε 4)~i1~i2~i3~i4 A_i1_i2_i3 (ε 4)_i4_j1_j2_j3))))

; *(dt^dx) = -dy^dz
(hodge (wedge [| 1 0 0 0 |] [| 0 1 0 0 |]))
;[| [| 0 0 0 0 |] [| 0 0 0 0 |] [| 0 0 0 -1 |] [| 0 0 0 0 |] |]

; *(dy^dz) = dt^dx
(hodge (wedge [| 0 0 1 0 |] [| 0 0 0 1 |]))
;[| [| 0 1 0 0 |] [| 0 0 0 0 |] [| 0 0 0 0 |] [| 0 0 0 0 |] |]

;(normalize2 (d [| (φ t x y z) (Ax t x y z) (Ay t x y z) (Az t x y z) |]))
;[|[| 0 (+ (Ax|1 t x y z) (* -1 (φ|2 t x y z))) (+ (Ay|1 t x y z) (* -1 (φ|3 t x y z))) (+ (Az|1 t x y z) (* -1 (φ|4 t x y z))) |]
;  [| (+ (* -1 (Ax|1 t x y z)) (φ|2 t x y z)) 0 (+ (Ay|2 t x y z) (* -1 (Ax|3 t x y z))) (+ (Az|2 t x y z) (* -1 (Ax|4 t x y z))) |]
;  [| (+ (* -1 (Ay|1 t x y z)) (φ|3 t x y z)) (+ (* -1 (Ay|2 t x y z)) (Ax|3 t x y z)) 0 (+ (Az|3 t x y z) (* -1 (Ay|4 t x y z))) |]
;  [| (+ (* -1 (Az|1 t x y z)) (φ|4 t x y z)) (+ (* -1 (Az|2 t x y z)) (Ax|4 t x y z)) (+ (* -1 (Az|3 t x y z)) (Ay|4 t x y z)) 0 |] |]

;(normalize3 (d (d [| (φ t x y z) (Ax t x y z) (Ay t x y z) (Az t x y z) |])))
;(tensor {4 4 4} {0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0} )

;(normalize1 (δ (d [| (φ t x y z) (Ax t x y z) (Ay t x y z) (Az t x y z) |])))
;[|(+ (* 6 (φ|4|4 t x y z)) (* -6 (Az|1|4 t x y z)) (* 6 (φ|3|3 t x y z)) (* -6 (Ay|1|3 t x y z)) (* 6 (φ|2|2 t x y z)) (* -6 (Ax|1|2 t x y z)))
;  (+ (* 6 (Ax|4|4 t x y z)) (* -6 (Az|2|4 t x y z)) (* 6 (Ax|3|3 t x y z)) (* -6 (Ay|2|3 t x y z)) (* 6 (φ|1|2 t x y z)) (* -6 (Ax|1|1 t x y z)))
;  (+ (* 6 (Ay|4|4 t x y z)) (* -6 (Az|3|4 t x y z)) (* -6 (Ax|2|3 t x y z)) (* 6 (Ay|2|2 t x y z)) (* 6 (φ|1|3 t x y z)) (* -6 (Ay|1|1 t x y z)))
;  (+ (* -6 (Ay|3|4 t x y z)) (* 6 (Az|3|3 t x y z)) (* -6 (Ax|2|4 t x y z)) (* 6 (Az|2|2 t x y z)) (* 6 (φ|1|4 t x y z)) (* -6 (Az|1|1 t x y z))) |]

(define $F
  [|[| 0 (Ex t x y z) (Ey t x y z) (Ez t x y z) |]
    [| (* -1 (Ex t x y z)) 0 (Bz t x y z) (* -1 (By t x y z)) |]
    [| (* -1 (Ey t x y z)) (* -1 (Bz t x y z)) 0 (Bx t x y z) |]
    [| (* -1 (Ez t x y z)) (By t x y z) (* -1 (Bx t x y z)) 0 |]
    |])

(normalize3 (d F))
;(tensor {4 4 4} {0 0 0 0 0 0 (/ (+ (* -1 (Bz|1 t x y z)) (Ey|2 t x y z) (* -1 (Ex|3 t x y z))) 3) (/ (+ (By|1 t x y z) (Ez|2 t x y z) (* -1 (Ex|4 t x y z))) 3) 0 (/ (+ (Bz|1 t x y z) (* -1 (Ey|2 t x y z)) (Ex|3 t x y z)) 3) 0 (/ (+ (* -1 (Bx|1 t x y z)) (Ez|3 t x y z) (* -1 (Ey|4 t x y z))) 3) 0 (/ (+ (* -1 (By|1 t x y z)) (* -1 (Ez|2 t x y z)) (Ex|4 t x y z)) 3) (/ (+ (Bx|1 t x y z) (* -1 (Ez|3 t x y z)) (Ey|4 t x y z)) 3) 0 0 0 (/ (+ (Bz|1 t x y z) (* -1 (Ey|2 t x y z)) (Ex|3 t x y z)) 3) (/ (+ (* -1 (By|1 t x y z)) (* -1 (Ez|2 t x y z)) (Ex|4 t x y z)) 3) 0 0 0 0 (/ (+ (* -1 (Bz|1 t x y z)) (Ey|2 t x y z) (* -1 (Ex|3 t x y z))) 3) 0 0 (/ (+ (* -1 (Bx|2 t x y z)) (* -1 (By|3 t x y z)) (* -1 (Bz|4 t x y z))) 3) (/ (+ (By|1 t x y z) (Ez|2 t x y z) (* -1 (Ex|4 t x y z))) 3) 0 (/ (+ (Bx|2 t x y z) (By|3 t x y z) (Bz|4 t x y z)) 3) 0 0 (/ (+ (* -1 (Bz|1 t x y z)) (Ey|2 t x y z) (* -1 (Ex|3 t x y z))) 3) 0 (/ (+ (Bx|1 t x y z) (* -1 (Ez|3 t x y z)) (Ey|4 t x y z)) 3) (/ (+ (Bz|1 t x y z) (* -1 (Ey|2 t x y z)) (Ex|3 t x y z)) 3) 0 0 (/ (+ (Bx|2 t x y z) (By|3 t x y z) (Bz|4 t x y z)) 3) 0 0 0 0 (/ (+ (* -1 (Bx|1 t x y z)) (Ez|3 t x y z) (* -1 (Ey|4 t x y z))) 3) (/ (+ (* -1 (Bx|2 t x y z)) (* -1 (By|3 t x y z)) (* -1 (Bz|4 t x y z))) 3) 0 0 0 (/ (+ (By|1 t x y z) (Ez|2 t x y z) (* -1 (Ex|4 t x y z))) 3) (/ (+ (* -1 (Bx|1 t x y z)) (Ez|3 t x y z) (* -1 (Ey|4 t x y z))) 3) 0 (/ (+ (* -1 (By|1 t x y z)) (* -1 (Ez|2 t x y z)) (Ex|4 t x y z)) 3) 0 (/ (+ (* -1 (Bx|2 t x y z)) (* -1 (By|3 t x y z)) (* -1 (Bz|4 t x y z))) 3) 0 (/ (+ (Bx|1 t x y z) (* -1 (Ez|3 t x y z)) (Ey|4 t x y z)) 3) (/ (+ (Bx|2 t x y z) (By|3 t x y z) (Bz|4 t x y z)) 3) 0 0 0 0 0 0} )

;(∇ B) = 0
;(rot x E) = ∂t B
;(rot y E) = ∂t B
;(rot z E) = ∂t B

(normalize1 (δ F))
;[|(+ (* -12 (Ez|4 t x y z)) (* -12 (Ey|3 t x y z)) (* -12 (Ex|2 t x y z)))
;  (+ (* 12 (By|4 t x y z)) (* -12 (Bz|3 t x y z)) (* -12 (Ex|1 t x y z)))
;  (+ (* -12 (Bx|4 t x y z)) (* 12 (Bz|2 t x y z)) (* -12 (Ey|1 t x y z)))
;  (+ (* 12 (Bx|3 t x y z)) (* -12 (By|2 t x y z)) (* -12 (Ez|1 t x y z))) |]

;(∇ E) = 0
;(rot x B) = ∂t E
;(rot y B) = ∂t E
;(rot z B) = ∂t E