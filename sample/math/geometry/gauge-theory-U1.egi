(define $N 4)

(define $g [| [| -1 0 0 0 |] [| 0 1 0 0 |] [| 0 0 1 0 |] [| 0 0 0 1 |] |])

(define $d
  (lambda [%X]
    !((flip ∂/∂) [| t x y z |] X)))

(define $hodge0
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 0)_i1_i2_i3_i4 A)))))

(define $hodge1
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 1)_i1_i2_i3_i4 A_j1 g~i1~j1)))))

(define $hodge2
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 2)_i1_i2_i3_i4 A_j1_j2 g~i1~j1 g~i2~j2)))))

(define $hodge3
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2 j3}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 3)_i1_i2_i3_i4 A_j1_j2_j3 g~i1~j1 g~i2~j2 g~i3~j3)))))

(define $hodge4
  (lambda [%A]
    (with-symbols {i1 i2 i3 i4 j1 j2 j3}
      (* (sqrt (abs (M.det g_#_#)))
         (. (ε' 4 4)_i1_i2_i3_i4 A_j1_j2_j3_j4 g~i1~j1 g~i2~j2 g~i3~j3 g~i4~j4)))))

(define $hodge
  (lambda [%A]
    (match (tensor-order A) integer
      {[,0 (hodge0 A)]
       [,1 (hodge1 A)]
       [,2 (hodge2 A)]
       [,3 (hodge3 A)]
       [,4 (hodge4 A)]})))

(define $δ
  (lambda [%A]
    (let {[$r (tensor-order A)]}
      (* (** -1 (+ (* N r) 1))
         (hodge (d (hodge A)))))))

(define $grad d)
(define $rot d)
(define $div δ)

(define $Δ
  (lambda [%A]
    (match (tensor-order A) integer
      {[,0 (δ (d A))]
       [,4 (d (δ A))]
       [_ (+ (d (δ A)) (δ (d A)))]})))

(define $normalize2
  (lambda [%A]
    (with-symbols {t1 t2}
      (- A_t1_t2 A_t2_t1))))

; *(dt^dx) = -dy^dz
(hodge (wedge [| 1 0 0 0 |] [| 0 1 0 0 |]))
;[| [| 0 0 0 0 |] [| 0 0 0 0 |] [| 0 0 0 -1 |] [| 0 0 0 0 |] |]

; *(dy^dz) = dt^dx
(hodge (wedge [| 0 0 1 0 |] [| 0 0 0 1 |]))
;[| [| 0 1 0 0 |] [| 0 0 0 0 |] [| 0 0 0 0 |] [| 0 0 0 0 |] |]

(normalize2 (d [| (φ t x y z) (Ax t x y z) (Ay t x y z) (Az t x y z) |]))
;[|[| 0 (+ (Ax|1 t x y z) (* -1 (φ|2 t x y z))) (+ (Ay|1 t x y z) (* -1 (φ|3 t x y z))) (+ (Az|1 t x y z) (* -1 (φ|4 t x y z))) |]
;  [| (+ (φ|2 t x y z) (* -1 (Ax|1 t x y z))) 0 (+ (Ay|2 t x y z) (* -1 (Ax|3 t x y z))) (+ (Az|2 t x y z) (* -1 (Ax|4 t x y z))) |]
;  [| (+ (φ|3 t x y z) (* -1 (Ay|1 t x y z))) (+ (Ax|3 t x y z) (* -1 (Ay|2 t x y z))) 0 (+ (Az|3 t x y z) (* -1 (Ay|4 t x y z))) |]
;  [| (+ (φ|4 t x y z) (* -1 (Az|1 t x y z))) (+ (Ax|4 t x y z) (* -1 (Az|2 t x y z))) (+ (Ay|4 t x y z) (* -1 (Az|3 t x y z))) 0 |]|]

(define $F
  [|[| 0 (Ex t x y z) (Ey t x y z) (Ez t x y z) |]
    [| (* -1 (Ex t x y z)) 0 (Bz t x y z) (* -1 (By t x y z)) |]
    [| (* -1 (Ey t x y z)) (* -1 (Bz t x y z)) 0 (Bx t x y z) |]
    [| (* -1 (Ez t x y z)) (By t x y z) (* -1 (Bx t x y z)) 0 |]
    |])

(hodge (d F))
;[|(+ (* -2 (Bz|4 t x y z)) (* -2 (By|3 t x y z)) (* -2 (Bx|2 t x y z)))
;  (+ (* -2 (Ey|4 t x y z)) (* 2 (Ez|3 t x y z)) (* -2 (Bx|1 t x y z)))
;  (+ (* 2 (Ex|4 t x y z)) (* -2 (Ez|2 t x y z)) (* -2 (By|1 t x y z)))
;  (+ (* -2 (Ex|3 t x y z)) (* 2 (Ey|2 t x y z)) (* -2 (Bz|1 t x y z)))|]

;(∇ B) = 0
;(rot x E) = ∂t B
;(rot y E) = ∂t B
;(rot z E) = ∂t B

(δ F)
;[|(+ (* -2 (Ez|4 t x y z)) (* -2 (Ey|3 t x y z)) (* -2 (Ex|2 t x y z)))
;  (+ (* 2 (By|4 t x y z)) (* -2 (Bz|3 t x y z)) (* -2 (Ex|1 t x y z)))
;  (+ (* -2 (Bx|4 t x y z)) (* 2 (Bz|2 t x y z)) (* -2 (Ey|1 t x y z)))
;  (+ (* 2 (Bx|3 t x y z)) (* -2 (By|2 t x y z)) (* -2 (Ez|1 t x y z)))|]

;(∇ E) = 0
;(rot x B) = ∂t E
;(rot y B) = ∂t E
;(rot z B) = ∂t E