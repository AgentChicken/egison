;;;
;;; Polar coordinates
;;;

(define $x [|θ φ r|])

(define $p [|(* r (sin θ) (cos φ))
             (* r (sin θ) (sin φ))
             (* r (cos θ))|])

;;
;; Local coordinates
;;

(define $e ((∂/∂ p $) x))
e
;[|[| (* r (cos θ) (cos φ)) (* -1 r (sin θ) (sin φ)) (* (sin θ) (cos φ)) |]
;  [| (* r (cos θ) (sin φ)) (* r (sin θ) (cos φ)) (* (sin θ) (sin φ)) |]
;  [| (* -1 r (sin θ)) 0 (cos θ) |] |]

;;
;; Metric tensor
;;

(define $g__ (generate-tensor 2#(V.* e_%1 e_%2) {3 3}))
(define $g~~ (with-symbols {i j} (/ (unit-tensor {3 3})_i_j g_i_j)))

g_#_#;[| [| r^2 0 0 |] [| 0 (* r^2 (sin θ)^2) 0 |] [| 0 0 1 |] |]
g~#~#;[| [| (/ 1 r^2) 0 0 |] [| 0 (/ 1 (* r^2 (sin θ)^2)) 0 |] [| 0 0 1 |] |]

;;
;; Christoffel symbols
;;

(define $Γ~__ (generate-tensor
                3#(with-symbols {j}
                    (* (/ 1 2) (. g~%1~j (+ (∂/∂ g_j_%3 x_%2) (∂/∂ g_j_%2 x_%3) (* -1 (∂/∂ g_%2_%3 x_j))))))
                {2 2 2}))

Γ~#_#_#;(tensor {2 2 2} {0 0 0 (* -1 (sin θ) (cos θ)) 0 (/ (cos θ) (sin θ)) (/ (cos θ) (sin θ)) 0} )

;;
;; Riemann curvature tensor
;;

(define $R~___ (generate-tensor
                 4#(with-symbols {j}
                     (+ (- (∂/∂ Γ~%1_%2_%3 x_%4) (∂/∂ Γ~%1_%2_%4 x_%3))
                        (- (. Γ~j_%2_%3 Γ~%1_j_%4) (. Γ~j_%2_%4 Γ~%1_j_%3))
                        ))
                 {2 2 2 2}))

R~#_#_#_#;(tensor {2 2 2 2} {0 0 0 0 0 (* -1 (sin θ)^2) (sin θ)^2 0 0 1 -1 0 0 0 0 0} )

;;
;; Ricci curvature
;;

(define $Ric__ (with-symbols {i j k} (contract + R~i_j_k_i)))

Ric_#_#;[| [| 1 0 |] [| 0 (sin θ)^2 |] |]

;;
;; Scalar curvature
;;

(define $scalar-curvature (with-symbols {j k} (. (generate-tensor 2#g~%1~%2 {2 2})~j~k Ric_j_k)))

scalar-curvature;(/ 2 r^2)
