;;;
;;; Polar coordinates
;;;

(define $x [|θ φ r|])

(define $X [|(* r (sin θ) (cos φ)) ; = x
             (* r (sin θ) (sin φ)) ; = y
             (* r (cos θ))         ; = z
             |])

;;
;; Local coordinates
;;

(define $e ((∂/∂ X $) x))
e
;[|[|(* r (cos θ) (cos φ)) (* r (cos θ) (sin φ)) (* -1 r (sin θ))|]
;  [|(* -1 r (sin θ) (sin φ)) (* r (sin θ) (cos φ)) 0|]
;  [|(* (sin θ) (cos φ)) (* (sin θ) (sin φ)) (cos θ)|]|]

;;
;; Metric tensor
;;

(define $g__ (generate-tensor 2#(V.* e_%1 e_%2) {2 2}))
(define $g~~ (with-symbols {i j} (/ (unit-tensor {2 2})_i_j g_i_j)))

g_#_#;[| [| r^2 0 |] [| 0 (* r^2 (sin θ)^2) |] |]_#_#
g~#~#;[| [| (/ 1 r^2) 0 |] [| 0 (/ 1 (* r^2 (sin θ)^2)) |] |]~#~#

;;
;; Christoffel symbols
;;

(define $Γ~__ (generate-tensor
                3#(with-symbols {j}
                    (* (/ 1 2) (. g~%1~j (+ (∂/∂ g_j_%3 x_%2) (∂/∂ g_j_%2 x_%3) (* -1 (∂/∂ g_%2_%3 x_j))))))
                {2 2 2}))

Γ~#_#_#;(tensor {2 2 2} {0 0 0 (* -1 (sin θ) (cos θ)) 0 (/ (cos θ) (sin θ)) (/ (cos θ) (sin θ)) 0} )~#_#_#
Γ~1_#_#;[| [| 0 0 |] [| 0 (* -1 (sin θ) (cos θ)) |] |]_#_#
Γ~2_#_#;[| [| 0 (/ (cos θ) (sin θ)) |] [| (/ (cos θ) (sin θ)) 0 |] |]_#_#

;;
;; Riemann curvature tensor
;;

(define $R~___ (generate-tensor
                 4#(with-symbols {j}
                     (+ (- (∂/∂ Γ~%1_%2_%3 x_%4) (∂/∂ Γ~%1_%2_%4 x_%3))
                        (- (. Γ~j_%2_%3 Γ~%1_j_%4) (. Γ~j_%2_%4 Γ~%1_j_%3))
                        ))
                 {2 2 2 2}))

R~#_#_#_#;(tensor {2 2 2 2} {0 0 0 0 0 (* -1 (sin θ)^2) (sin θ)^2 0 0 1 -1 0 0 0 0 0} )~#_#_#_#
R~1_1_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#
R~1_2_#_#;[| [| 0 (* -1 (sin θ)^2) |] [| (sin θ)^2 0 |] |]_#_#
R~2_1_#_#;[| [| 0 1 |] [| -1 0 |] |]_#_#
R~2_2_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#

(define $R____ (with-symbols {i} (. g_i_# R~i_#_#_#)))

R_#_#_#_#;(tensor {2 2 2 2} {0 0 0 (* r^2 (sin θ)^2) 0 0 0 0 (* r^2 (sin θ)^2) 0 (* r^2 (sin θ)^2) (* r^2 (sin θ)^2) 0 0 0 0} )_#_#_#_#
R_1_1_#_#;[| [| 0 0 |] [| 0 (* r^2 (sin θ)^2) |] |]_#_#
R_1_2_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#
R_2_1_#_#;[| [| (* r^2 (sin θ)^2) 0 |] [| (* r^2 (sin θ)^2) (* r^2 (sin θ)^2) |] |]_#_#
R_2_2_#_#;[| [| 0 0 |] [| 0 0 |] |]_#_#


;;
;; Ricci curvature
;;

(define $Ric__ (with-symbols {i j k} (contract + R~i_j_k_i)))

Ric_#_#;[| [| 1 0 |] [| 0 (sin θ)^2 |] |]_#_#

;;
;; Scalar curvature
;;

(define $scalar-curvature (with-symbols {j k} (. (generate-tensor 2#g~%1~%2 {2 2})~j~k Ric_j_k)))

scalar-curvature;(/ 2 r^2)
